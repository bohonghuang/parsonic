(in-package #:parsonic)

(defun lexical->unit-args (form &optional blocks)
  (if (consp form)
      (destructuring-case form
        (((parser/funcall parser/apply) function &rest parsers)
         `(,(car form) ,(walk-parsers-in-lambda (rcurry #'lexical->unit-args blocks) function)
           . ,(mapcar (rcurry #'lexical->unit-args blocks) parsers)))
        ((parser/unit signature body)
         (declare (ignore signature))
         (lexical->unit-args body (cons form blocks))
         form)
        ((parser/let name bindings body)
         (declare (ignore name))
         (loop :for binding :in bindings
               :for (nil val) := (ensure-list binding)
               :when (and val (symbolp val))
                 :do (loop :for block :in blocks
                           :for (type signature bindings) := block
                           :if (eq type 'parser/unit)
                             :collect signature :into signatures
                           :else
                             :if (eq type 'parser/let)
                               :if (find val bindings :key (compose #'car #'ensure-list))
                                 :return (loop :for signature :in signatures
                                               :do (pushnew val (second signature) :key (compose #'car #'ensure-list)))))
         (lexical->unit-args body (cons form blocks))
         form)
        ((t &rest args) (cons (car form) (mapcar (rcurry #'lexical->unit-args blocks) args))))
      form))

(defvar *intermediate-unit-p*)

(defun remove-intermediate-units (form)
  (if (boundp '*intermediate-unit-p*)
      (if (consp form)
          (destructuring-case form
            (((parser/cons parser/list parser/rep) &rest args)
             (when args (setf *intermediate-unit-p* t))
             (cons (car form) (mapcar #'remove-intermediate-units args)))
            (((parser/funcall parser/apply) function &rest parsers)
             (let ((*intermediate-unit-p* nil))
               `(,(car form) ,(walk-parsers-in-lambda
                               (lambda (form)
                                 (let ((*intermediate-unit-p* nil))
                                   (remove-intermediate-units form)))
                               function)
                 . ,(mapcar #'remove-intermediate-units parsers))))
            ((parser/filter (lambda lambda-list &rest body) &rest parsers)
             (assert (eq lambda 'lambda))
             `(,(car form) (lambda ,lambda-list . ,body)
               . ,(loop :with function := `(lambda ,(remove nil lambda-list) . ,body)
                        :with variable-arg-index := (when (function-identity-p function) (position nil lambda-list :test-not #'eq))
                        :for parser :in parsers
                        :for index :from 0
                        :if (eql index variable-arg-index)
                          :collect (remove-intermediate-units parser)
                        :else
                          :collect (let ((*intermediate-unit-p* nil))
                                     (remove-intermediate-units parser)))))
            ((parser/unit signature body)
             (let ((*intermediate-unit-p* nil))
               (let ((body (remove-intermediate-units body)))
                 (if *intermediate-unit-p* body `(parser/unit ,signature ,body)))))
            ((t &rest args) (cons (car form) (mapcar #'remove-intermediate-units args))))
          form)
      (let ((*intermediate-unit-p* nil))
        (remove-intermediate-units form))))

(defvar *parser-units*)
(defvar *parser-unit-parent* nil)

(defstruct parser-unit
  (parents nil :type list)
  (children nil :type list)
  (count 0 :type non-negative-fixnum)
  (tree nil :type list)
  (tree-cost 0 :type non-negative-fixnum))

(defun parser-tree-cost (tree)
  (typecase tree
    ((cons symbol list)
     (+ (if (eql (search (string '#:parser/) (symbol-name (car tree))) 0) 1 0)
        (parser-tree-cost (car tree)) (parser-tree-cost (cdr tree))))
    (cons (+ (parser-tree-cost (car tree)) (parser-tree-cost (cdr tree))))
    (t 0)))

(defun parser-unit-cost (unit)
  (if (plusp (parser-unit-tree-cost unit))
      (parser-unit-tree-cost unit)
      (setf (parser-unit-tree-cost unit) (parser-tree-cost (parser-unit-tree unit)))))

(defun (setf parser-unit-cost) (value unit)
  (assert (plusp value))
  (setf (parser-unit-tree-cost unit) value))

(defun parser-unit-mapc (function unit)
  (funcall function unit)
  (mapc (lambda (unit) (parser-unit-mapc function unit)) (parser-unit-children unit)))

(defun parser-unit-walk (form)
  (if (consp form)
      (destructuring-case form
        ((parser/unit signature parser)
         (let* ((unit (ensure-gethash signature *parser-units* (make-parser-unit)))
                (tree (let ((*parser-unit-parent* (when (zerop (parser-unit-count unit)) unit)))
                        (parser-unit-walk parser))))
           (when *parser-unit-parent*
             (push unit (parser-unit-children *parser-unit-parent*))
             (pushnew *parser-unit-parent* (parser-unit-parents unit)))
           (incf (parser-unit-count unit))
           (setf (parser-unit-tree unit) (or (parser-unit-tree unit) `(parser/unit ,signature ,tree)))))
        ((t &rest args)
         (declare (ignore args))
         (cons (car form) (mapcar #'parser-unit-walk (cdr form)))))
      form))

(defparameter *parser-unit-extract-threshold-count* 2)
(defparameter *parser-unit-extract-threshold-cost* 16)
(defparameter *parser-unit-extract-threshold-total-cost* 64)
(defparameter *parser-unit-extract-recursive-p* t)

(defun extract/compile (form)
  (loop :with units := (let ((*parser-units* (make-hash-table :test #'equal)))
                         (setf form (parser-unit-walk (lexical->unit-args (remove-intermediate-units form))))
                         (hash-table-values *parser-units*))
        :and functions := nil
        :for (unit) := (setf units (loop :for unit :in (sort units #'> :key #'parser-unit-cost)
                                         :for count := (parser-unit-count unit)
                                         :and cost := (parser-unit-cost unit)
                                         :when (and (>= count *parser-unit-extract-threshold-count*)
                                                    (>= cost *parser-unit-extract-threshold-cost*)
                                                    (>= (* count cost) *parser-unit-extract-threshold-cost*))
                                           :collect unit))
        :while unit
        :do (destructuring-bind (parser/unit (name lambda-list) parser) (parser-unit-tree unit)
              (assert (eq parser/unit 'parser/unit))
              (let ((name (gensym (format nil "窿磲疸狎ㄣ镯痫箦＇汜＇孱篚蝈扉篝钺礤┅┅ㄡ蜱灬礅溽扉篝狎珲礤铘灬礅溽扉篝┅瘐箬扉篝钺礤狎珞疳蝮弪骢钽糸镱螬戾è泔躅ㄩ疳蝮弪躅轸屮趄徙舡蝈沲蝮轹瀛皙ū疳蝮弪躅轸泔躅躅轸┅疳蝮弪躅轸泔躅躅轸┅┅疳蝮弪躅轸磲疸灬礅溽躅轸ㄤ邈疳蝮弪躅轸泔躅躅轸泔躅舂躅轸┅ㄡ篌弪疳蝮弪躅轸泔躅躅轸疳蝮弪躅轸屮趄徙舡翳蝈箬镬洵泔躅舄┅灬忮祗è蝈沲躅轸箝瀛蝈漉沐洎麒孱痨躞箝瀛蝈漉沐洎祜镳烘矧疳蝈铘洪疳蝮弪躅轸疳蝈铘躅轸烘矧麸翎飙箝瀛蝈漉沐航祜镳烘矧汨殪洪疳蝮弪躅轸汨殪潋孱疳蝈铘瑚桢ㄥ汨殪躅轸后蹴箝瀛蝈漉沐洎轰ㄤ邈疳蝮弪躅轸泔篝疳蝈铘麸翎飙箝瀛蝈漉沐洎蝈沲疳蝈铘麸翎飙箝瀛蝈漉沐洎┅┅蝈沲躅轸ū疳蝮弪躅轸泔篝躅轸┅┅祜镳烘矧疳蝈铘洪疳蝮弪躅轸疳蝈铘躅轸轰ㄤ屐弭彐疳蝮弪躅轸汨殪潋孱疳蝈铘躅轸┅箦翩ㄣ狎疳蝮弪躅轸趄邋躅轸┅ю狎箦虔汜祆ㄣ潋疳蝮弪躅轸趄邋躅轸┅ㄣ镱钺礤狎珞┅┅烘轭犰禊蝈趱蝾鲠祯弩骘蝽骢钽糸镱螬┅